plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 32
    ndkVersion '21.4.7075529'

    defaultConfig {
        applicationId "com.brouken.player"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 107
        versionName "0.107"
        archivesBaseName = "Just.Player.v${versionName}"
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "targetSdk", "distribution"
    productFlavors {
        latest {
            dimension "targetSdk"
        }
        legacy {
            dimension "targetSdk"
            applicationIdSuffix ".legacy"
            versionNameSuffix "-legacy"
            targetSdkVersion 29
        }

        universal {
            dimension "distribution"
        }
        amazon {
            dimension "distribution"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("legacy") && names.contains("amazon")) {
            setIgnore(true)
        }
    }
    androidComponents {
        onVariants(selector().withFlavor(new kotlin.Pair("distribution", "amazon")), {
            packaging.dex.useLegacyPackaging.set(false)
            packaging.jniLibs.excludes.add('lib/armeabi-v7a/*_neon.so')
        })
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // conflicts with Android dependencies
    configurations {
        all*.exclude group: "xmlpull", module: "xmlpull"
        all*.exclude group: "org.apache.commons", module: "commons-compress"
        all*.exclude group: "com.thoughtworks.xstream", module: "commons-compress"
    }
    lint {
        disable 'MissingTranslation'
    }
}

dependencies {
    def exoplayerVersion = '2.17.1'
    implementation("com.google.android.exoplayer:exoplayer-datasource:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-decoder:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-database:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:extension-mediasession:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-common"
    }
    implementation("com.google.android.exoplayer:exoplayer-dash:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-smoothstreaming:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation("com.google.android.exoplayer:exoplayer-rtsp:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    api ('com.google.guava:guava:31.0.1-android') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'com.ibm.icu:icu4j:70.1'
    implementation 'com.arthenica:ffmpeg-kit-https:4.5.1-1.LTS'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'io.github.pilgr:paperdb:2.7.2'
    api group: 'org.boofcv', name: 'boofcv-core', version: '0.31'
    api group: 'org.boofcv', name: 'boofcv-android', version: '0.31'
    implementation project(path: ':doubletapplayerview')
    implementation project(path: ':android-file-chooser')
    implementation project(path: ':android-smbfile-chooser')
    implementation fileTree(dir: "libs", include: ["extension-*.aar"])
    implementation fileTree(dir: "libs", include: ["library-*.aar"])
}